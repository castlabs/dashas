buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.gradlefx', name: 'gradlefx', version: '0.8.2'
    }
}

apply plugin: 'gradlefx'
apply plugin: 'flashbuilder'

type = 'swf'
version = '1.0'
mainClass = 'com/castlabs/dash/DashPlugin.as'

repositories {
    ivy {
        name 'Apache'
        artifactPattern 'http://archive.apache.org/dist/incubator/flex/[revision]-incubating/binaries/[module]-[revision]-incubating-bin.[ext]'
    }
    maven {
        url "http://dashas.castlabs.com/maven2/"
    }
}

dependencies {
    flexSDK group: 'org.apache', name: 'apache-flex-sdk', version: '4.8.0', ext: 'tar.gz'
    test group: 'org.flexunit', name: 'flexUnitTasks', version: '4.2.0', ext: 'jar'
    test group: 'org.flexunit', name: 'flexunit', version: '4.2.0', ext: 'swc'
    test group: 'org.flexunit', name: 'flexunit-cilistener', version: '4.2.0', ext: 'swc'
    test group: 'org.flexunit', name: 'flexunit-uilistener', version: '4.2.0', ext: 'swc'
    test group: 'mockolate', name: 'mockolate', version: '0.12.4', ext: 'swc'
    test group: 'org.hamcrest', name: 'hamcrest', version: '1.1.3', ext: 'swc'
}

sdkAutoInstall {
    showPrompts = false
}

// if a profile isn't defined, a debug is default
if (!project.hasProperty("profile")) {
    project.ext.profile = "debug"
}

apply from: "${profile}-profile.gradle"

task copySwfToDemo(type: Copy) {
    mustRunAfter compileFlex

    from file(project.copyFrom)
    into project.copyTo
}

task compile {
    dependsOn compileFlex
    dependsOn copySwfToDemo
}

def libDir="${rootProject.projectDir}/lib"

task exportDependencies << {
    ant.delete (dir:libDir, failonerror:false)
    configurations.each { config ->
        config.each { f ->
            ant.copy (file: f, overwrite:true, todir:libDir)
        }
    }
}